apply plugin: 'com.android.application'
apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.2'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

// Our merge report task
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'
    ])
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.a494studios.koreanconjugator"
        minSdkVersion 19
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 20
        versionName "2.0.3"
        testInstrumentationRunner "com.a494studios.koreanconjugator.HanjiTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }

    buildTypes.each {
        it.buildConfigField("String", "SERVER_URL", getApiKey("serverURL"))
        it.buildConfigField("String", "ADMOB_KEY", getApiKey("adMobKey"))
        it.buildConfigField("String", "SLACK_KEY", getApiKey("slackKey"))
        it.resValue("string", "DISPLAY_AD_ID", getApiKey("displayAdID"))
        it.resValue("string", "MAIN_AD_ID", getApiKey("mainAdID"))
        it.resValue("string", "SEARCH_AD_ID", getApiKey("searchAdID"))
        it.resValue("string", "SEARCH_RESULTS_AD_ID", getApiKey("searchResultsAdID"))
        it.resValue("string", "CONJ_INFO_AD_ID", getApiKey("conjInfoAdID"))
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

//return a MY API KEY from a properties file.
def getApiKey(String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("keys.txt"))
    return "\"" + properties.getProperty(property) + "\""
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation 'com.github.frankiesardo:linearlistview:1.0.1@aar'
    implementation 'com.codemybrainsout.onboarding:onboarder:1.0.4'

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.4.3'
    implementation 'com.google.firebase:firebase-ads:19.2.0'
    implementation 'com.google.firebase:firebase-messaging:20.2.1'
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'

    // Favorites parsing
    implementation 'com.google.code.gson:gson:2.8.5'
    api 'com.google.guava:guava:28.0-android'

    // Settings, About, Feedback
    implementation 'com.codemybrainsout.rating:ratingdialog:1.0.8'
    implementation 'com.github.eggheadgames:android-about-box:2.0.1'
    implementation 'com.mikepenz:aboutlibraries:8.3.1'
    implementation 'org.rm3l:maoni:6.0.0'
    implementation 'org.rm3l:maoni-common:6.0.0'
    implementation 'com.github.pschroen:slack-api-android:c66cc8d997'

    // Apollo
    implementation 'com.apollographql.apollo:apollo-runtime:1.2.3'
    implementation 'com.apollographql.apollo:apollo-http-cache:1.0.1'
    implementation 'com.apollographql.apollo:apollo-rx2-support:1.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Google Play Billing
    implementation 'com.android.billingclient:billing:3.0.0'

    // Testing dependencies
    implementation 'androidx.test.espresso:espresso-contrib:3.3.0' // Needed for idler
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.robolectric:robolectric:4.2.1' // Can't be upgraded until #5454 is fixed
    testImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.8.1'
    androidTestUtil 'androidx.test:orchestrator:1.3.0'
}
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.apollographql.android'
apply plugin: 'com.google.firebase.crashlytics'
